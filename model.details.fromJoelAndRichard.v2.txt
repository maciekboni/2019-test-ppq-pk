;; 1. Based on: run119
;; 2. Description: No Health effect secondary
;; x1. Author: user
;;----------------------------------------------------------------------------
;;NONMEM model template created by Joel Tarning (MORU) - uppdated 2011-09-27
;;----------------------------------------------------------------------------
;;Modeller: Richard Höglund
;;Project name: Piperaquine meta-analysis
;;date: 2014-08-28
;;----------------------------------------------------------------------------
$PROBLEM    87
$INPUT      ID STUD=DROP SIDN STU1 STU2 STU3 STU4 STU5 STU6 STU7 STU8
            STU9 ST10 PID=DROP TIME NDV=DROP LOQ=DROP BLOQ=DROP EVID
            MDV DV CMT RATE=DROP OCC1=DROP OCC2=DROP OCC3=DROP WT AGE
            AGE2=DROP PREG COUN=DROP MAL AMT MAT SEX WORL OCC
            ADDL=DROP II=DROP
$DATA       Pooled_ext2_secondary.csv IGNORE=# IGNORE(SIDN==1)
$SUBROUTINE ADVAN6 TOL=6 ;TRANS1
$MODEL
COMP=(1) ;Abs
COMP=(2) ;Central
COMP=(3) ;Per1
COMP=(4) ;Per2
COMP=(5) ;TRANS1
COMP=(6) ;TRANS2
$PK

ACL = 0.75
AV = 1
M_WE = 54

OC1=0
OC2=0
OC3=0
IF(OCC.EQ.1) OC1=1
IF(OCC.EQ.2) OC2=1
IF(OCC.EQ.3) OC3=1

IOV = ETA(10)*OC1+ETA(11)*OC2+ETA(12)*OC3
IOV2 = ETA(13)*OC1+ETA(14)*OC2+ETA(15)*OC3

EM50 = THETA(11)
HILL = THETA(12)
MF = ((AGE)**HILL)/(((EM50)**HILL)+((AGE)**HILL))

F1D = THETA(10)
F1COVD = (1 + F1D*(OCC-1))

TVCL = THETA(1)*MF*(WT/M_WE)**ACL;
TVV2 = THETA(2)*(WT/M_WE)**AV;
TVQ1 = THETA(3)*(WT/M_WE)**ACL;
TVV3 = THETA(4)*(WT/M_WE)**AV;
TVQ2 = THETA(5)*(WT/M_WE)**ACL;
TVV4 = THETA(6)*(WT/M_WE)**AV;
TVMT = THETA(7);
TVF1 = THETA(8)*F1COVD;

CL = TVCL*EXP(ETA(1));
V2 = TVV2*EXP(ETA(2));
Q1 = TVQ1*EXP(ETA(3));
V3 = TVV3*EXP(ETA(4));
Q2 = TVQ2*EXP(ETA(5));
V4 = TVV4*EXP(ETA(6));
MT = TVMT*EXP(ETA(7)+IOV2);
F1 = TVF1*EXP(ETA(8)+IOV);

NN = 2
KTR = (NN+1)/MT

K15 = KTR
K56 = KTR
K62 = KTR
K23 = Q1/V2
K32 = Q1/V3
K24 = Q2/V2
K42 = Q2/V4
K20 = CL/V2

S2 = V2*1000
VD = V2+V3+V4


$DES ###MFB these are the 6 differential equations for the 6 compartments
DADT (1) = -A(1)*K15
DADT (2) = A(6)*K62 - A(2)*K20 - A(2)*K23 + A(3)*K32 - A(2)*K24 + A(4)*K42
DADT (3) = A(2)*K23 - A(3)*K32
DADT (4) = A(2)*K24 - A(4)*K42

DADT (5) = A(1)*K15 - A(5)*K56
DADT (6) = A(5)*K56 - A(6)*K62

CT=A(2)/S2               ; (or other expression for concentration)

$ERROR
IPRE = A(2)/S2
IF(MAT.EQ.1) IPRE = IPRE+IPRE*THETA(9)
IF(IPRE.GT.0) IPRE = LOG(IPRE)
IPRED=IPRE

W     = SQRT(SIGMA(1,1))
;IF(W.EQ.0) W=1
IRES  = DV-IPRED
;IWRES = IRES/W
Y     = IPRED+EPS(1)*EXP(ETA(9))

$THETA  ###MFB the second number in parentheses below is the MLE from Joel&Richard's fitting exercise
(0, 55.4) ; 1.CL/F L/h
(0, 2910) ; 2.V2/F  L
(0, 310) ; 3.Q1/F L/h
(0, 4910) ; 4.V3/F  L
(0, 105) ; 5.Q2/F L/h
(0, 30900) ; 6.V4/F  L
(0, 2.11) ; 7. MT
(1) FIX ; 8. F
(1.06) ; 9. Scale
(-0.99, 0.236) ; 12 F dos occ
(0, 0.575) ;ET50
(0, 5.51) ;Hill

$OMEGA 0.0752 ;         CL
 0.371 ;         V2
 0 FIX ;         Q1    ### MFB ETA3 is fixed at zero
 0.0558 ;         V3
 0.0541 ;         Q2
 0.114 ;         V4
 0.135 ;         MT ### MFB this is for ETA7
 0.158              ### MFB this is for ETA8
 0 FIX              ### MFB this is for ETA9 -- it has a variance of zero
$OMEGA  BLOCK(1)
 0.252 ;      IOV F         ### MFB this is for ETA10, ETA11, ETA12 
$OMEGA  BLOCK(1) SAME
$OMEGA  BLOCK(1) SAME
$OMEGA  BLOCK(1)
 0.195 ;    IOV MTT         ### MFB this is for ETA13, ETA14, ETA15 
$OMEGA  BLOCK(1) SAME
$OMEGA  BLOCK(1) SAME

$SIGMA 0.115

$ESTIMATION POSTHOC MAXEVAL=0 NSIG=2 SIGL=6 METHOD=1 INTER PRINT=1
