Please use the following reference:  
WWARN NONMEM Code 1 file used to develop: Artemether-lumefantrine dosing for malaria treatment in young children and pregnant women: a pharmacokinetic-pharmacodynamic meta-analysis. 
F. Kloprogge, L. Workman, S. Borrmann, M. Tékété, G. Lefèvre, K. Hamed, P. Piola, J. Ursing, P.E. Kofoed, A. Mårtensson, B. Ngasala, A. Björkman, M. Ashton, S.F. Hietala, F. Aweeka, S. Parikh, L. Mwai, T.M.E. Davis, H. Karunajeewa, S. Salman, F. Checchi, C. Fogg, P.N. Newton, M. Mayxay, P. Deloron, J.F. Faucher, F. Nosten, E.A. Ashley, R. McGready, M. van Vugt, S. Proux, R.N. Price, J. Karbwang, F. Ezzet, R. Bakshi,  K. Stepniewska, N.J. White, P.J. Guerin, K.I. Barnes, J. Tarning. 
PLoS Medicine. 2018 Jun 12;15(6):e1002579.
Read the full summary of the Antimalarial – Lumefantrine POP/PK Study Group

// NOTE MFB : this is the code for the 2-compartment model which is the one used in the Kloprogge paper;
// NOTE MFB : the two "disposition" compartments are the central compartment (x2) and the peripheral compartment (x3),
// NOTE MFB : and there is one absorption compartment (x1) that doesn't get counted in the compartment type;
// NOTE MFB : 
// NOTE MFB : so, the core of the equations for the PK of lumefantrine will look like:
// NOTE MFB : 
// NOTE MFB :     dx1/dt  =  [DOSE]  -  k_a x1
// NOTE MFB :     dx2/dt  =  k_a x1  +  k_32 x3  -  k x2  -  k_23 x2
// NOTE MFB :     dx3/dt  =  k_23 x2  -  k_32 x3 
// NOTE MFB : 
// NOTE MFB : and the code below tells you which of these get variation added to them;
// NOTE MFB : there is no inter-dose variation in this model; there is only inter-patient variability



$INPUT     
           ID 	; Patient ID
           TIME 	; Time of sample 
           DV 	; Dependent variable (natural logarithm of observed concentrations, nmol/L)
           WT 	; Body weight (covariate) 
           EVID 	; Event ID record 
           MDV 	; Missing dependent variable (1=missing)
           AMT 	; Dose amount (μmol)
           CMT 	; Compartment (1=dose, 2=lumefantrine) 
           PREG 	; Pregnancy (covariate; 0=non-pregnant, 1=pregnant) 
           LNPC 	; Parasite count (covariate; logarithm of parasite count)
           DOSE 	; Lumefantrine dosage (covariate; mg/kg)

$DATA  
           dataset.csv  IGNORE=#

$SUBROUTINE  
           ADVAN4 TRANS1


$PK
;------------------------------------Dose covariate----------------------------------------------------------------------


       D50 = THETA(7) 		; Dose (mg/kg) to reach 50% of full saturation effect
       DS = 1 - (DOSE/(DOSE+D50) ) 		; Dose (mg/kg) covariate relationship 
;------------------------------------------------------------------------------------------------------------------------------------

;------------------------------------ Pregnancy covariate ----------------------------------------------------------------
// NOTE MFB : the variable "PREG" is one for a pregnant woman in second/third trimester (according to the data)
// NOTE MFB : the variable is zero otherwise
// NOTE MFB : 
// NOTE MFB : reminder: the THETAs are the parameters to be estimated

         PREGNANCY = (1 + THETA(8) * PREG)	; Linear covariate relationship for pregnancy

;------------------------------------------------------------------------------------------------------------------------------------

;------------------------------------Parasite biomass covariate-------------------------------------------------------------
// NOTE MFB : LNPC is the log parasite count TODO ask Richard if it is log10 or log_e and if the count is per/ul or something else
// NOTE MFB : 
           PARASITE = ((LNPC /4.20)**THETA(9))	; Power covariate relationship for parasite biomass
;------------------------------------------------------------------------------------------------------------------------------------

           TVCL	= THETA(1) * ((WT/42)**0.75) 	; Population clearance
           CL	= TVCL * EXP(ETA(1)) 	; Individual clearance 

           TVV	= THETA(2) * ((WT/42)**1)	; Population central volume
           V	= TVV * EXP(ETA(2))	; Individual central volume

           TVQ	= THETA(3) * ((WT/42)**0.75) 	; Population inter-compartment clearance
           Q	= TVQ * EXP(ETA(3))	; Individual inter-compartment clearance

           TVVP	= THETA(4) * ((WT/42)**1)	; Population peripheral volume 
           VP	= TVVP * EXP(ETA(4))	; Individual peripheral volume 

           TVKA	= THETA(5) * PREGNANCY	; Population absorption rate constant
           KA	= TVKA * EXP(ETA(5))	; Individual absorption rate constant

           TVF1	= THETA(6) * DS * PARASITE	; Population relative bioavailability
           BXPAR 	= THETA(10)	; Box-Cox shape parameter
           PHI	= EXP(ETA(6))	; Exponential of IIV
           ETATR 	= (PHI**BXPAR-1)/BXPAR	; Box-Cox tranfomation of IIV
           F1	= TVF1 * EXP(ETATR)	; Individual relative bioavailability

           K 	= CL/V	; Elimination rate constant (COMP 2 --> 0)
           K23 	= Q/V	; Distribution rate constant (COMP 2 --> 3)
           K32 	= Q/VP	; Distribution rate constant (COMP 3 --> 2)
           S2 	= V /1000	; Scaling for central volume
           
$ERROR
          IPRED = A(2) / S2		; Predicted plasma concentration
           IF(IPRED.GT.0)  IPRED = LOG(IPRED)	; Natural logarithm of predictions
           W = SQRT(SIGMA(1,1))		; Residual error
           IRES = IPRED – DV	; Individual residual error
           IWRES = IRES / W		; Individual weighted residual error
           Y = IPRED + EPS(1)		; Additive residue error
;------------------------------------------------------------------------------------------------------------------------------------
// NOTE MFB : in the code below, the THETAs are assigned a
// NOTE MFB :   ( LOWER_BOUNDARY, INITIAL_ESTIMATE )
// NOTE MFB : in the search procedure.

$THETA	; Initial estimates of theta
            (0, 1.35)	; 1. Clearance
            (0, 11.2)	; 2. Central volume of distribution
            (0, 0.344)	; 3. Inter-compartment clearance
            (0, 59.0)	; 4. Peripheral volume of distribution
            (0, 0.0386)	; 5. Absoprtion rate constant
            (1 FIX)	; 6. Relative bioavailability
            (0, 3.86)	; 7. Dose (mg/kg) to reach 50% of full absoption 
            (-1, 0.352)	; 8. Pregnancy on absoprtion rate constant
            (-1, -0.643)	; 9. Parasite biomass on relative bioavailability
            (-1, -0.343)	; 10. Box-Cox shape parameter

$OMEGA	; Initial estimates for omega
           (0 FIX)	; 1. IIV clearance
           (1.12)	; 2. IIV central volume of distribution
           (0  FIX)	; 3. IIV Inter-compartment clearance
           (0 FIX)	; 4. IIV peripheral volume of distribution
           (0 FIX)	; 5. IIV absorption rate constant
           (0.402)	; 6. IIV relative bioavailability

$SIGMA	; Initial estimates of sigma
           (0.323)	; Residual variability 

$ESTIMATION   POSTHOC MAXEVAL=9999   METHOD=1 INTER 
